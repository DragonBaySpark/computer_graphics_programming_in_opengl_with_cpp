# 1. 光照
## 1.1. 光源
| 光源类型   | 特点                                                                 | 优点                                                                 | 缺点                                                                 |
|------------|----------------------------------------------------------------------|----------------------------------------------------------------------|----------------------------------------------------------------------|
| **环境光** | 整个场景均匀受光，无方向和位置。                                      | 模拟全局光照，避免完全黑暗的区域。                                   | 缺乏方向性和真实感，无法产生阴影。                                   |
| **平行光** | 光线方向平行，无位置，仅有方向。                                      | 计算简单，适合模拟太阳光等远距离光源。                               | 无法模拟局部光照，缺乏真实感。                                       |
| **点光源** | 光线从一个点向四周发散，具有位置和方向。                              | 能模拟局部光照，产生阴影和立体感。                                   | 计算复杂，光照强度随距离衰减，性能开销较大。                         |
| **聚光灯** | 光线从一个点沿特定方向发散，形成锥形光照区域。                        | 能模拟手电筒、舞台灯等聚焦光源，光照区域可控。                       | 计算复杂，需要额外判断光照角度和范围。                               |
| **半球光** | 模拟天空光照，顶部光线为一种颜色，底部光线为另一种颜色。              | 能模拟自然光的渐变效果，适合户外场景。                               | 计算复杂度较高，通常需要结合其他光源使用。                           |
| **区域光** | 光线从一个平面或区域发散，具有方向和范围。                            | 能模拟窗户、屏幕等光源，光照效果更真实。                             | 计算复杂度高，通常需要光照贴图或全局光照算法支持。                   |
### 1.1.1. 环境光
环境光 基本原理是，场景中所有方向上都有光，只是强度不同。全局环境光可以定义如下：
```cpp
float globalAmbient[4] = { 0.6f, 0.6f, 0.6f, 1.0f };
```

### 1.1.2. 平行光

![](https://easyimage.elyt.cn/i/2025/04/18/5725067152642281522-2.webp)  
平行光的基本原理是，所有的光都从同一个方向照射到物体上，这个方向就是平行光的方向。
平行光无位置，只有方向。它可以用来模拟光源距离非常远，以至于光线接近平行的情况，例如阳光

指向 z 轴负方向的红色定向光可以指定如下：
```cpp
float dirLightAmbient[4] = { 0.1f, 0.0f, 0.0f, 1.0f }; 
float dirLightDiffuse[4] = { 1.0f, 0.0f, 0.0f, 1.0f }; 
float dirLightSpecular[4] = { 1.0f, 0.0f, 0.0f, 1.0f }; 
float dirLightDirection[3] = { 0.0f, 0.0f, -1.0f };
```

在已经有全局环境光的情况下，定向光的环境光分量看起来似乎是多余的。然而，当光源“开启”或“关闭”时，全局环境光和定向光的环境光分量的区别就很明显了。当“开启”时，总环境光分量将如预期的那样增加。在上面的例子中，我们只使用了很小的环境光分量。在实际场景中，应当根据场景的需要平衡两个环境光分量
### 1.1.3. 点光源
点光源的基本原理是，所有的光都从一个点向各个方向照射到物体上，这个点就是点光源的位置。  
![](https://easyimage.elyt.cn/i/2025/04/18/5725067383375140253-2.webp)  

#### 1.1.3.1. 点光源的特点
点光源是计算机图形学和现实照明中常见的一种光源类型，它模拟了从一个点向各个方向均匀发射光线的光源。以下是点光源的主要特点：

##### 1.1.3.1.1. 物理特性方面
1. **位置决定性**：点光源有明确的位置坐标，所有光线从该点向四面八方发射。在三维空间中，其位置可以用一个三维向量 `(x, y, z)` 来精确表示。在场景中移动点光源的位置，会直接改变物体受光的区域和强度分布。
2. **光线发散性**：光线从点光源出发，呈辐射状向周围空间发散传播。随着传播距离的增加，光线覆盖的面积会逐渐增大，这符合光的传播规律。
3. **光照衰减**：点光源的光照强度会随着距离的增加而减弱。根据平方反比定律，光照强度与距离的平方成反比，即距离点光源越远，物体接收到的光照越弱。

##### 1.1.3.1.2. 渲染效果方面
1. **产生阴影**：由于点光源的光线是从一个点发出的，物体被其照射时会在背后产生明显的阴影。阴影的形状和范围取决于物体的形状、位置以及点光源的位置和强度。
2. **多角度照明**：能从多个角度照亮物体，使物体表面产生丰富的明暗变化，增强物体的立体感和层次感。物体朝向点光源的面会被照亮，而背向的面则处于阴影中。
3. **局部照明效果**：点光源通常用于突出场景中的特定物体或区域，营造局部照明效果。例如，在游戏场景中，可以用点光源模拟火把、吊灯等，使这些发光物体周围的环境更加生动。

##### 1.1.3.1.3. 计算复杂度方面
1. **计算量较大**：与平行光等简单光源相比，点光源的光照计算更为复杂。因为需要考虑光源位置、物体与光源的距离、光照衰减等多个因素，所以在实时渲染中，大量使用点光源可能会对性能产生较大影响。
2. **可优化性**：为了提高渲染效率，有多种针对点光源的优化算法，如光照探针、光照贴图等。这些方法可以在一定程度上减少点光源的计算量，同时保持较好的光照效果。 

#### 1.1.3.2. 点光源的实现步骤
点光源的实现步骤如下：
1. 计算点光源到物体表面的距离。
2. 计算点光源到物体表面的方向向量。
3. 计算点光源到物体表面的光照强度。
4. 将光照强度应用到物体表面。


点光源具有指定为 RGBA 值的环境光反射、漫反射和镜面反射特性。
位置(5,2,−3)处的红色位置光可以指定如下：
```cpp
float posLightAmbient[4] = { 0.1f, 0.0f, 0.0f, 1.0f }; 
float posLightDiffuse[4] = { 1.0f, 0.0f, 0.0f, 1.0f }; 
float posLightSpecular[4] = { 1.0f,0.0f, 0.0f, 1.0f }; 
float posLightLocation[3] = { 5.0f, 2.0f, -3.0f };
float radius=1.0f;
```

常见的点光源衰减因子的公式及其特点：
| 衰减因子公式                                | 名称               | 特点                                                                                     | 优点                                                                                     | 缺点                                                                                     |
|---------------------------------------------|--------------------|------------------------------------------------------------------------------------------|------------------------------------------------------------------------------------------|------------------------------------------------------------------------------------------|
| `attenuation = 1.0`                         | 无衰减             | 光照强度恒定，不随距离变化。                                                             | 计算简单，适合小范围光照。                                                               | 不符合物理规律，无法模拟真实光照效果。                                                   |
| `attenuation = 1.0 / distance`              | 线性衰减           | 光照强度与距离成反比。                                                                   | 计算简单，能模拟一定的衰减效果。                                                         | 衰减过快，远距离光照效果较弱。                                                           |
| `attenuation = 1.0 / (distance * distance)` | 平方反比衰减       | 光照强度与距离的平方成反比，符合物理规律。                                               | 模拟真实光照效果，适合大范围光照。                                                       | 距离较近时光照强度过强，可能导致过曝。                                                   |
| `attenuation = 1.0 / (1.0 + k * distance)`  | 线性修正衰减       | 在线性衰减的基础上加入常数项，避免距离为零时光照强度无限大。                             | 计算简单，适合实时渲染。                                                                 | 衰减效果可能不够真实。                                                                   |
| `attenuation = 1.0 / (1.0 + k1 * distance + k2 * distance * distance)` | 线性+平方修正衰减 | 综合线性衰减和平方法，加入常数项，常用于实时渲染。                                        | 平衡了近距离和远距离的光照强度，适合大多数场景。                                           | 参数调整较复杂，需要根据场景手动调节 `k1` 和 `k2`。                                       |
| `attenuation = max(0.0, 1.0 - (distance / radius))` | 截断式衰减        | 光照强度在一定范围内线性衰减，超出范围后强度为零。                                       | 适合模拟局部光照，计算简单。                                                             | 不符合物理规律，光照范围外会突然变暗。                                                   |
| `attenuation = exp(-k * distance)`          | 指数衰减           | 光照强度随距离指数级减弱。                                                               | 模拟柔和的光照衰减效果，适合特定场景。                                                   | 衰减过快，远距离光照效果几乎不可见。                                                     |
| `attenuation = 1.0 / (1.0 + k1 * distance + k2 * distance * distance + k3 * distance^3)` | 高阶多项式衰减    | 在线性+平方修正衰减的基础上加入三次项，模拟更复杂的光照衰减效果。                        | 能模拟更真实的光照效果，适合高质量渲染。                                                 | 计算复杂度较高，通常用于离线渲染。                                                       |

线性衰减：
```cpp
float distance=length(posLightLocation-fragPos);
//float attenuation=1.0/(1.0+radius*distance+radius*radius);
float attenuation=max(0.0,1.0-(distance/radius));
vec3 adjLightColor=lightColor*attenuation;
```

### 1.1.4. 聚光灯

![](https://easyimage.elyt.cn/i/2025/04/19/5725264691937153988-2.webp)  
聚光灯是一种特殊的光源，它从某个方向照射物体，并在物体表面产生锥形的光照区域。
![](https://easyimage.elyt.cn/i/2025/05/06/5731372791643509368-2.webp)  
位于(5,2,−3)向下照射 z 轴负方向的红色聚光灯可以表示为：

```cpp
float spotLightAmbient[4] = { 0.1f, 0.0f, 0.0f, 1.0f }; 
float spotLightDiffuse[4] = { 1.0f, 0.0f, 0.0f, 1.0f }; 
float spotLightSpecular[4] = { 1.0f,0.0f, 0.0f, 1.0f }; 
float spotLightLocation[3] = { 5.0f, 2.0f, -3.0f }; 
float spotLightDirection[3] = { 0.0f, 0.0f, -1.0f }; 
float spotLightCutoff = 20.0f; 
float spotLightExponent = 10.0f;
```

常见的聚光灯衰减因子的公式及其特点： 注意：**光离轴角必须小于截光角，超过则聚光灯将不会产生光照效果**。
| 衰减因子公式                                | 名称               | 特点                                                                                     | 优点                                                                                     | 缺点                                                                                     |
|---------------------------------------------|--------------------|------------------------------------------------------------------------------------------|------------------------------------------------------------------------------------------|------------------------------------------------------------------------------------------|
| `attenuation = 1.0`                         | 无衰减             | 光照强度恒定，不随距离变化。                                                             | 计算简单，适合小范围光照。                                                               | 不符合物理规律，无法模拟真实光照效果。                                                   |
| `attenuation = 1.0 / distance`              | 线性衰减           | 光照强度与距离成反比。                                                                   | 计算简单，能模拟一定的衰减效果。                                                         | 衰减过快，远距离光照效果较弱。                                                           |
| `attenuation = 1.0 / (distance * distance)` | 平方反比衰减       | 光照强度与距离的平方成反比，符合物理规律。                                               | 模拟真实光照效果，适合大范围光照。                                                       | 距离较近时光照强度过强，可能导致过曝。                                                   |
| `attenuation = 1.0 / (1.0 + k * distance)`  | 线性修正衰减       | 在线性衰减的基础上加入常数项，避免距离为零时光照强度无限大。                             | 计算简单，适合实时渲染。                                                                 | 衰减效果可能不够真实。                                                                   |
| `attenuation = 1.0 / (1.0 + k1 * distance + k2 * distance * distance)` | 线性+平方修正衰减 | 综合线性衰减和平方法，加入常数项，常用于实时渲染。                                        | 平衡了近距离和远距离的光照强度，适合大多数场景。                                           | 参数调整较复杂，需要根据场景手动调节 `k1` 和 `k2`。                                       |
| `attenuation = max(0.0, 1.0 - (distance / radius))` | 截断式衰减        | 光照强度在一定范围内线性衰减，超出范围后强度为零。                                       | 适合模拟局部光照，计算简单。                                                             | 不符合物理规律，光照范围外会突然变暗。                                                   |
| `attenuation = exp(-k * distance)`          | 指数衰减           | 光照强度随距离指数级减弱。                                                               | 模拟柔和的光照衰减效果，适合特定场景。                                                   | 衰减过快，远距离光照效果几乎不可见。                                                     |
| `attenuation = cos(theta)`                  | 角度衰减           | 光照强度与光线方向和片段方向的夹角余弦值成正比。                                         | 模拟聚光灯的锥形光照区域，适合手电筒、舞台灯等场景。                                       | 需要额外判断角度范围，计算复杂度较高。                                                   |
| `attenuation = pow(cos(theta), exponent)`   | 指数角度衰减       | 在角度衰减的基础上加入指数项，控制光照强度的分布。                                       | 能模拟更柔和的聚光灯光照效果，适合高质量渲染。                                             | 参数调整复杂，计算复杂度较高。                                                           |


聚光灯的实现步骤如下：
1. 计算聚光灯到物体表面的距离。
2. 计算聚光灯到物体表面的方向向量。
3. 计算聚光灯到物体表面的光照强度。
4. 计算聚光灯到物体表面的方向向量与聚光灯方向的角度，可以转换为余弦值。即：cosθ = dot(lightDir,lightDirToFragment)
5. 判断聚光灯是否能照射到物体上，即cosθ是否大于聚光灯的lightCutoff值（角度<聚光灯的角度）。
6. 将光照强度应用到物体表面。




以下是运行效果：
从中可以看到，聚光灯的光照区域是一个圆形的区域。
![](https://easyimage.elyt.cn/i/2025/04/21/5726053629454983060-2.webp)  





## 1.2. 光照模型
| 光照模型   | 特点                                                                 | 优点                                                                 | 缺点                                                                 |
|------------|----------------------------------------------------------------------|----------------------------------------------------------------------|----------------------------------------------------------------------|
| **环境光** | 整个场景均匀受光，无方向和位置。                                      | 模拟全局光照，避免完全黑暗的区域。                                   | 缺乏方向性和真实感，无法产生阴影。                                   |
| **漫反射光** | 光线从光源照射到物体表面，光强与光线与表面法线夹角的余弦值成正比。      | 模拟物体表面的基本光照效果，增强立体感。                             | 无法模拟高光效果，缺乏真实感。                                       |
| **镜面反射光** | 光线从光源照射到物体表面后，沿特定方向反射，产生高光效果。              | 模拟光滑表面的高光效果，增强真实感。                                 | 计算复杂度较高，依赖视角位置。                                       |
| **Phong 光照模型** | 结合环境光、漫反射光和镜面反射光，模拟物体表面的综合光照效果。         | 光照效果较为真实，适合大多数场景。                                   | 计算复杂度较高，性能开销较大。                                       |
| **Blinn-Phong 光照模型** | Phong 模型的优化版本，使用半程向量计算镜面反射光。                 | 性能较高，适合实时渲染。                                             | 高光效果可能不如 Phong 模型精确。                                    |
| **Cook-Torrance 光照模型** | 基于物理的光照模型，考虑微表面结构和 Fresnel 效应。               | 光照效果非常真实，适合高质量渲染。                                   | 计算复杂度极高，通常用于离线渲染。                                   |
| **Toon 光照模型** | 非真实感光照模型，使用离散的光照强度模拟卡通风格。                    | 模拟卡通风格，适合特定艺术风格的场景。                               | 不适合真实感场景，光照效果较为简单。                                 |
| **Oren-Nayar 光照模型** | 模拟粗糙表面的漫反射光，考虑表面微结构的影响。                     | 更真实地模拟粗糙表面的光照效果。                                     | 计算复杂度较高，性能开销较大。                                       |
| **Minnaert 光照模型** | 模拟暗表面或背光场景的光照效果，光强与视角和光源方向相关。           | 适合模拟月球表面等特殊场景的光照效果。                               | 不适合一般场景，光照效果较为特殊。                                   |


## 1.3. 材质 
每个物体都有其独特的材质，材质决定了物体对光的各种特性。通党我们用 四个参数：环境光反射、漫反射、镜面反射、光泽来描述材质。要模拟锡铅合金的效果，可以指定如下值：
```cpp
float pewterMatAmbient[4] = { .11f, .06f, .11f, 1.0f }; 
float pewterMatDiffuse[4] = { .43f, .47f, .54f, 1.0f }; 
float pewterMatSpecular[4] = { .33f, .33f, .52f, 1.0f }; 
float pewterMatShininess = 9.85f;
```
![](https://easyimage.elyt.cn/i/2025/05/06/5731589920917560073-2.webp)  


## 1.4. Phong光照 
这是一个运行效果：  
![1](https://easyimage.elyt.cn/i/2025/05/06/5731409000554041603.gif)


Phong光照模型是一种经典的光照模型，用于模拟真实世界中的光照效果，它将光照分为环境光（Ambient）、漫反射光（Diffuse）和镜面反射光（Specular）三个部分。最终的光照颜色是这三部分光照颜色的总和。
![](https://easyimage.elyt.cn/i/2025/05/01/5729625211335086683-2.webp)  

Phong Reflection Model 来自 [维基百科](https://en.wikipedia.org/wiki/Phong_reflection_model)上的数据

下面详细介绍各部分的数学公式及整体公式。

### 1.4.1. 环境光（Ambient）
环境光模拟了场景中全局的、均匀的光照，它不依赖于光源的位置和物体的朝向。环境光的计算公式如下：
$$
I_{ambient} = k_a \times I_a \times M_a
$$
其中：

- $I_{ambient}$ 是环境光的颜色强度。
- $k_a$ 是物体的环境光反射系数，取值范围通常在 $[0, 1]$ 之间，它表示物体对环境光的反射能力。
- $I_a$ 是环境光的颜色强度。
- $M_a$ 是材质的环境光颜色强度。
### 1.4.2. 漫反射光（Diffuse）
![](https://easyimage.elyt.cn/i/2025/05/06/5731598107259443503-2.webp)  
漫反射光模拟了光线在物体表面的均匀散射，它取决于光线的方向和物体表面法线的夹角。漫反射光的计算公式基于 Lambert 余弦定律：
$$
I_{diffuse} = k_d \times I_d \times M_d \times \max(0, \mathbf{N} \cdot \mathbf{L})
$$
其中：

- $I_{diffuse}$ 是漫反射光的颜色强度。
- $k_d$ 是物体的漫反射系数，取值范围通常在 $[0, 1]$ 之间，它表示物体对漫反射光的反射能力。
- $I_d$ 是光源的颜色强度。
- $M_d$ 是材质的漫反射颜色强度。
- $\mathbf{N}$ 是物体表面的法线向量，且为单位向量。
- $\mathbf{L}$ 是从物体表面指向光源的单位向量。
- $\mathbf{N} \cdot \mathbf{L}$ 是 $\mathbf{N}$ 和 $\mathbf{L}$ 的点积，$\max(0, \mathbf{N} \cdot \mathbf{L})$ 确保结果不会为负数。
### 1.4.3. 镜面反射光（Specular）
![](https://easyimage.elyt.cn/i/2025/05/06/5731598234678206403-2.webp)  
镜面反射光模拟了光线在物体表面的镜面反射效果，它取决于观察者的位置。镜面反射光的计算公式如下：
$$
I_{specular} = k_s \times I_s \times M_s \times (\max(0, \mathbf{R} \cdot \mathbf{V}))^n
$$
其中：

- $I_{specular}$ 是镜面反射光的颜色强度。
- $k_s$ 是物体的镜面反射系数，取值范围通常在 $[0, 1]$ 之间，它表示物体对镜面反射光的反射能力。
- $I_s$ 是光源的颜色强度。
- $M_s$ 是材质的反射光颜色强度。
- $\mathbf{R}$ 是反射光线的单位向量，可以通过公式 $\mathbf{R} = 2(\mathbf{N} \cdot \mathbf{L})\mathbf{N} - \mathbf{L}$ 计算得到 。GLSL 中采用函数reflect()来计算。
- $\mathbf{V}$ 是从物体表面指向观察者的单位向量。
- $n$ 是高光指数（Shininess），它控制了镜面反射的范围，值越大，高光越集中。
![](https://easyimage.elyt.cn/i/2025/05/06/5731598459346099628-2.webp)  
以余弦指数建模的反光度  

#### 1.4.3.1. Phong光照模型整体公式
最终的光照颜色 $I_{total}$ 是环境光、漫反射光和镜面反射光的总和：
$$
I_{total} = I_{ambient} + I_{diffuse} + I_{specular}
$$
将前面的公式代入可得：
$$
I_{total} = k_a \times I_a \times M_a + k_d \times I_d \times M_d \times \max(0, \mathbf{N} \cdot \mathbf{L}) + k_s \times I_s \times M_s \times (\max(0, \mathbf{R} \cdot \mathbf{V}))^n
$$
上述是一个非常简单的 Phong 光照模型，它只考虑了环境光、漫反射光和镜面反射光三个部分，且漫反射光和镜面反射光均只有一个光源。
如果是有多个光源，则需要分别计算每个光源对物体的光照贡献，然后将它们相加得到最终的光照颜色。  
![](https://easyimage.elyt.cn/i/2025/05/01/5729626903615116956-2.webp) 


代码之间的简要逻辑关系如下：
![](https://easyimage.elyt.cn/i/2025/05/06/5731597777440346315-2.webp)

**法线矩阵**（Normal Matrix）是用于将法线向量从模型空间（Model Space）变换到世界空间（World Space）的矩阵。
由于法线向量表示的是表面的方向，它的变换规则和普通的位置向量有所不同。当模型矩阵包含非均匀缩放时，直接使用模型矩阵变换法线向量会导致法线方向错误，因此需要使用法线矩阵来确保法线向量在变换后仍然垂直于物体表面。
法线矩阵是模型矩阵的逆转置矩阵的左上角 3x3 子矩阵。数学表达式如下：
$$ [ M_{normal} = (M_{model}^{-1})^T ] $$

可以使用 glm 库中的函数来计算法线矩阵。以下是一个示例代码：

```c++
mat3 normalMatrix = transpose(inverse(mat3(model)));
```
在实际应用中，特别是在使用4x4矩阵处理变换（如模型视图矩阵或模型视图投影矩阵）的情况下，有时会将3x3的法线矩阵扩展为4x4矩阵以适应现有的数学库或者为了与位置变换统一起来处理。这种情况下，通常是将3x3的法线矩阵放在4x4矩阵的左上角，并将第四行和第四列设置为(0, 0, 0, 1)。但请注意，这样做主要是出于方便的考虑，实际上只有左上角的3x3部分对法线向量的变换有效。

**从顶点着色器到片段着色器的向量，因为涉及到插值，可以不是单位向量，所以在片段着色器中使用前需要归一化**

### 顶点着色器
```cpp
// 指定GLSL版本为4.30
#version 430

// 顶点属性输入
layout (location = 0) in vec3 vertPos;    // 顶点位置
layout (location = 1) in vec3 vertNormal; // 顶点法线

// 输出到片段着色器的变量
out vec3 varyingNormal;    // 变换后的法线
out vec3 varyingLightDir;  // 光源方向
out vec3 varyingVertPos;   // 视图空间中的顶点位置

// 点光源结构体定义
struct PositionalLight
{   
    vec4 ambient;    // 环境光分量
    vec4 diffuse;    // 漫反射分量
    vec4 specular;   // 镜面反射分量
    vec3 position;   // 光源位置
};

// 材质结构体定义
struct Material
{   
    vec4 ambient;    // 材质的环境光反射系数
    vec4 diffuse;    // 材质的漫反射系数
    vec4 specular;   // 材质的镜面反射系数
    float shininess; // 镜面反射的光泽度
};

// uniform变量声明
uniform vec4 globalAmbient;      // 全局环境光
uniform PositionalLight light;    // 点光源属性
uniform Material material;        // 物体材质属性
uniform mat4 mv_matrix;          // 模型视图矩阵
uniform mat4 proj_matrix;        // 投影矩阵
uniform mat4 norm_matrix;        // 法线变换矩阵

void main(void)
{   
    // 计算顶点在视图空间中的位置
    varyingVertPos = (mv_matrix * vec4(vertPos,1.0)).xyz;
    
    // 计算从顶点到光源的方向向量
    varyingLightDir = light.position - varyingVertPos;
    
    // 变换法线到视图空间
    // 使用法线矩阵以正确处理非统一缩放
    varyingNormal = (norm_matrix * vec4(vertNormal,1.0)).xyz;

    // 计算裁剪空间中的顶点位置
    gl_Position = proj_matrix * mv_matrix * vec4(vertPos,1.0);
}
```

### 片段着色器
```cpp
// 指定GLSL版本为4.30
#version 430

// 从顶点着色器接收的插值数据
in vec3 varyingNormal;    // 插值后的法线向量
in vec3 varyingLightDir;  // 插值后的光源方向
in vec3 varyingVertPos;   // 插值后的顶点位置

// 输出的片段颜色
out vec4 fragColor;

// 点光源结构体定义
struct PositionalLight
{   
    vec4 ambient;    // 环境光分量
    vec4 diffuse;    // 漫反射分量
    vec4 specular;   // 镜面反射分量
    vec3 position;   // 光源位置
};

// 材质结构体定义
struct Material
{   
    vec4 ambient;    // 材质的环境光反射系数
    vec4 diffuse;    // 材质的漫反射系数
    vec4 specular;   // 材质的镜面反射系数
    float shininess; // 镜面反射的光泽度
};

// uniform变量声明
uniform vec4 globalAmbient;      // 全局环境光
uniform PositionalLight light;    // 点光源属性
uniform Material material;        // 物体材质属性
uniform mat4 mv_matrix;          // 模型视图矩阵
uniform mat4 proj_matrix;        // 投影矩阵
uniform mat4 norm_matrix;        // 法线变换矩阵

void main(void)
{   
    // 标准化光照计算所需的向量
    vec3 L = normalize(varyingLightDir);    // 光源方向向量
    vec3 N = normalize(varyingNormal);      // 表面法线向量
    vec3 V = normalize(-varyingVertPos);    // 视线向量（指向相机）
    
    // 计算反射向量R
    // reflect(-L, N)计算-L关于N的反射方向
    vec3 R = normalize(reflect(-L, N));
    
    // 计算光源方向与法线的夹角余弦值
    // 用于漫反射计算
    float cosTheta = dot(L,N);
    
    // 计算视线方向与反射光线的夹角余弦值
    // 用于镜面反射计算
    float cosPhi = dot(V,R);

    // 计算Phong光照模型的三个分量（每个像素）
    
    // 1. 环境光分量：考虑全局环境光和光源的环境光
    vec3 ambient = ((globalAmbient * material.ambient) + 
                    (light.ambient * material.ambient)).xyz;
    
    // 2. 漫反射分量：与表面法线和光源方向的夹角有关
    vec3 diffuse = light.diffuse.xyz * material.diffuse.xyz * 
                   max(cosTheta,0.0);
    
    // 3. 镜面反射分量：与反射向量和视线方向的夹角有关
    vec3 specular = light.specular.xyz * material.specular.xyz * 
                    pow(max(cosPhi,0.0), material.shininess);
    
    // 将三个分量相加得到最终颜色
    fragColor = vec4((ambient + diffuse + specular), 1.0);
}
```





## Blinn-Phong 光照模型
Blinn-Phong 光照模型是 Phong 光照模型的一个优化版本，它使用半程向量（Halfway Vector）来计算镜面反射光，从而减少了计算量。半程向量是光线和视线之间的中点，它与视线方向和光线方向都垂直。
Blinn 发现向量 R 在计算过程中并不是必需的——R 只是用来计算角 φ 的手段。角 φ 的计算
可以不使用向量 R，而通过 L 与 V 的角平分线向量 H 得到。如图 7.13 所示，H 和 N 之间的角 α
刚好等于 1/2(φ)。虽然 α 与 φ 不同，但 Blinn 展示了使用 α 代替 φ 就已经可以获得足够好的结果。
角平分线向量可以简单地使用 L+V 得到（见图 7.14）

![](https://easyimage.elyt.cn/i/2025/05/06/5731603204953609948-2.webp)  


### dolphin 模型


在计算半程向量时，我们使用 L+V 的结果除以它的长度，得到半程向量。由于 L+V 的长度是 1，所以半程向量的长度也是 1。
```cpp
    // 计算镜面反射分量
	//vec3 R = reflect(-L,N);
	vec3 halfVector = normalize(L + V);
	float specular = pow(max(dot(N,halfVector),0),material.shininess);
```

**运行结果**
![1](https://easyimage.elyt.cn/i/2025/05/08/5732143645587414481.gif)

**完整片段着色器代码**
```cpp
#version 430

// 点光源结构体定义
struct PositionalLight
{   
    vec4 ambient;    // 环境光分量
    vec4 diffuse;    // 漫反射分量
    vec4 specular;   // 镜面反射分量
    vec3 position;   // 光源位置
};

// 材质结构体定义
struct Material
{   
    vec4 ambient;    // 材质的环境光反射系数
    vec4 diffuse;    // 材质的漫反射系数
    vec4 specular;   // 材质的镜面反射系数
    float shininess; // 镜面反射的光泽度
};


in vec3 varyingNormal;
in vec3 varyingLightDir;
in vec3 varyingVertPos;


in vec2 tc;
out vec4 color;

uniform mat4 mv_matrix;
uniform mat4 proj_matrix;

uniform vec4 globalAmbient;      // 全局环境光

uniform PositionalLight light;    // 点光源属性
uniform Material material;        // 物体材质属性

uniform mat4 norm_matrix;        // 法线变换矩阵
layout (binding=0) uniform sampler2D s;

void main(void)
{
	vec4 texColor = texture(s,tc);

	// 标准化光照计算所需的向量
	vec3 N = normalize(varyingNormal);
	vec3 L = normalize(varyingLightDir);
	vec3 V = normalize(-varyingVertPos);

	// 计算漫反射分量
	float lambertTerm = max(0,dot(N,L));
	vec4 Id = light.diffuse * material.diffuse * lambertTerm;
	
	// 计算镜面反射分量
	//vec3 R = reflect(-L,N);
	vec3 halfVector = normalize(L + V);
	
	float specular = pow(max(dot(N,halfVector),0),material.shininess);
	vec4 Is = light.specular * material.specular * specular;

	// 计算环境光分量
	vec4 Ia = globalAmbient * material.ambient;

	// 计算最终颜色值
	color = texColor*(globalAmbient+light.ambient + light.diffuse*(max(dot(N,L),0.0f))) +light.specular*pow(max(dot(N,halfVector),0.0f),material.shininess);

}
```


## 1.5. 参考
1. [OpenGL shader开发实战学习笔记：第九章 第一个光照模型-CSDN博客](https://blog.csdn.net/weixin_44539328/article/details/147233750?spm=1011.2415.3001.5331)
2. [OpenGL shader开发实战学习笔记：第十二章 深入光照_shader 平行光-CSDN博客](https://blog.csdn.net/weixin_44539328/article/details/147402874?spm=1011.2415.3001.5331)
3. [学习笔记完整代码下载](https://github.com/DragonBaySpark/computer_graphics_programming_in_opengl_with_cpp)